using Dates
const DEFAULT_FREQUENCY = 250.0f0
const TIME_FORMAT = DateFormat("HH:MM:SS")
const DATE_FORMAT = DateFormat("DD/MM/YYYY")

@enum StorageFormat begin
    _8bit_first_difference = 8
    _16bit_twos_complement = 16
    _24bit_twos_complement_lsb = 24
    _32bit_twos_complement_lsb = 32
    _16bit_twos_complement_msb = 61
    _8bit_offset_binary = 80
    _16bit_offset_binary = 160
    _12bit_twos_complement = 212
    _10bit_twos_complement_sets_of_11 = 310
    _10bit_twos_complement_sets_of_4 = 311
end
abstract type AbstractStorageFormat end
abstract type format8 <: AbstractStorageFormat end
abstract type format16 <: AbstractStorageFormat end
abstract type format24 <: AbstractStorageFormat end
abstract type format32 <: AbstractStorageFormat end
abstract type format61 <: AbstractStorageFormat end
abstract type format80 <: AbstractStorageFormat end
abstract type format160 <: AbstractStorageFormat end
abstract type format212 <: AbstractStorageFormat end
abstract type format310 <: AbstractStorageFormat end
abstract type format311 <: AbstractStorageFormat end
struct WfdbFormat{T<:AbstractStorageFormat}
    digitalNaN::Union{Nothing,Int32}
end

WfdbFormat(s::StorageFormat) = WfdbFormat(Val{s})
function WfdbFormat(s::WfdbFormat)
    error("constuctor not implemented for $(typeof(s))")
end
WfdbFormat(::Type{Val{_12bit_twos_complement}}) = WfdbFormat{format212}(-2^11)
WfdbFormat(::Type{Val{_16bit_offset_binary}}) = WfdbFormat{format160}(-2^15)
WfdbFormat(::Type{Val{_16bit_twos_complement_msb}}) = WfdbFormat{format61}(-2^15)
WfdbFormat(::Type{Val{_16bit_twos_complement}}) = WfdbFormat{format16}(-2^15)
WfdbFormat(::Type{Val{_24bit_twos_complement_lsb}}) = WfdbFormat{format24}(-2^23)
WfdbFormat(::Type{Val{_32bit_twos_complement_lsb}}) = WfdbFormat{format32}(-2^31)
WfdbFormat(::Type{Val{_8bit_first_difference}}) = WfdbFormat{format8}(nothing)
WfdbFormat(::Type{Val{_8bit_offset_binary}}) = WfdbFormat{format80}(-2^7)
WfdbFormat(::Type{Val{_10bit_twos_complement_sets_of_11}}) = WfdbFormat{format310}(-2^9)
WfdbFormat(::Type{Val{_10bit_twos_complement_sets_of_4}}) = WfdbFormat{format311}(-2^9)

WfdbFormat(s::String) = parse(Int64, s) |> StorageFormat |> WfdbFormat

struct SignalSpecLine{T<:AbstractStorageFormat}
    filename::String
    format::WfdbFormat{T}
    samples_per_frame::Int32
    skew::UInt32
    byte_offset::UInt32
    adc_gain::Float32
    baseline::Int32
    units::String
    adc_resolution::UInt32
    adc_zero::Int32
    initial_value::Int32
    checksum::Union{Nothing,Int16}
    block_size::UInt32
    description::String
end

const SingleSpecVector = Vector{SignalSpecLine}
const MultiSpecVector = Vector{SingleSpecVector}
const UnionSpecVector = Union{SingleSpecVector,MultiSpecVector}

"""
    filename(s::SignalSpecLine)
a getter method for the filename field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
filename(s::SignalSpecLine) = getfield(s, :filename)
filename(xs::Vector{SignalSpecLine}) = [getfield(x, :filename) for x in xs]

"""
    format(s::SignalSpecLine)
a getter method for the format field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
format(s::SignalSpecLine) = getfield(s, :format)
format(xs::Vector{SignalSpecLine}) = [getfield(x , :format) for x in xs]

"""
    samples_per_frame(s::SignalSpecLine)
a getter method for the samples_per_frame field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
samples_per_frame(s::SignalSpecLine) = getfield(s, :samples_per_frame)
samples_per_frame(xs::Vector{SignalSpecLine}) = [getfield(x, :samples_per_frame) for x in xs]
samples_per_frame(xs::Vector{Vector{SignalSpecLine}}) = mapfoldl(samples_per_frame,vcat,xs)

"""
    skew(s::SignalSpecLine)
a getter method for the skew field in the header. can either be used on: #
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
skew(s::SignalSpecLine) = getfield(s, :skew)
skew(xs::Vector{SignalSpecLine}) =[getfield(x, :skew) for x in xs]

"""
    byte_offset(s::SignalSpecLine)
a getter method for the byte_offset field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
byte_offset(s::SignalSpecLine) = getfield(s, :byte_offset)
byte_offset(xs::Vector{SignalSpecLine}) =[getfield(x, :byte_offset) for x in xs]

"""
    adc_gain(s::SignalSpecLine)
a getter method for the adc_gain field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
adc_gain(s::SignalSpecLine) = getfield(s, :adc_gain)
adc_gain(xs::Vector{SignalSpecLine}) = (adc_gain(x) for x in xs) |> flatten |> collect
adc_gain(xs::Vector{Vector{SignalSpecLine}}) = (adx_gain(x) for x in xs) |> flatten |> collect

"""
    baseline(s::SignalSpecLine)
a getter method for the baseline field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
baseline(s::SignalSpecLine) = getfield(s, :baseline)
baseline(xs::Vector{SignalSpecLine}) = (baseline(x) for x in xs ) |> flatten |> collect
baseline(xs::Vector{Vector{SignalSpecLine}}) = ( baseline(x) for x in xs) |> flatten |> collect

"""
    units(s::SignalSpecLine)
a getter method for the units field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
units(s::SignalSpecLine) = getfield(s, :units)
units(xs::Vector{SignalSpecLine}) =[getfield(x, :units) for x in xs]

"""
    adc_resolution(s::SignalSpecLine)
a getter method for the adc_resolution field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
adc_resolution(s::SignalSpecLine) = getfield(s, :adc_resolution)
adc_resolution(xs::Vector{SignalSpecLine}) =[getfield(x, :adc_resolution) for x in xs]

"""
    adc_zero(s::SignalSpecLine)
a getter method for the adc_zero field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
adc_zero(s::SignalSpecLine) = getfield(s, :adc_zero)
adc_zero(xs::Vector{SignalSpecLine}) =[getfield(x, :adc_zero) for x in xs]

"""
    initial_value(s::SignalSpecLine)
a getter method for the initial_value field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
initial_value(s::SignalSpecLine) = getfield(s, :initial_value)
initial_value(xs::Vector{SignalSpecLine}) = [getfield(x, :initial_value) for x in xs]
initial_value(xs::Vector{Vector{SignalSpecLine}}) = mapfoldl(initial_value,vcat,xs)

"""
    checksum(s::SignalSpecLine)
a getter method for the checksum field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
checksum(s::SignalSpecLine) = getfield(s, :checksum)
checksum(xs::Vector{SignalSpecLine}) =[getfield(x, :checksum) for x in xs]

"""
    block_size(s::SignalSpecLine)
a getter method for the block_size field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
block_size(s::SignalSpecLine) = getfield(s, :block_size)
block_size(xs::Vector{SignalSpecLine}) = [getfield(x, :block_size) for x in xs]

"""
    description(s::SignalSpecLine)
a getter method for the description field in the header. can either be used on:
- Header
- SignalSpecLine
- Vector{SignalSpecLine}
"""
description(s::SignalSpecLine) = getfield(s, :description)
description(xs::Vector{SignalSpecLine}) =[getfield(x, :description) for x in xs]

#TODO: document
"""
    struct Header
record_name::String - A string of characters that identify the record. The record name
number_of_segments::Union{Nothing,UInt32}
  - If the field is present, it indicates that the record is a  multi-segment
    record containing the specified number of segments, and that the header file
    contains segment specification lines rather than signal specification lines.
number_of_signals::UInt32 - the number of signals
sampling_frequency::Float32 units samples/second/signal
counter_frequency::Float32
base_counter_value::Float32
samples_per_signal::Union{Nothing,UInt32}
base_time::Union{Nothing,Time}
base_date::Union{Nothing,Date}
parentdir::String
signal_specs::Vector{SignalSpecLine}

Example header (100.hea in sample-data directory of repo)
---------START OF FILE-----------
# unnecessary comment
100 2 360 650000                        <---all records up to signal_specs
100.dat 212 200 11 1024 995 -22131 0 MLII

100.dat 212 200 11 1024 1011 20052 0 V5
# 69 M 1085 1629 x1
# Aldomet, Inderal
---------END OF FILE-------------
"""
abstract type AbstractHeaderType end
abstract type MultiRecordHeader <: AbstractHeaderType end
abstract type SingleRecordHeader <: AbstractHeaderType end

# Maybe this should be split out into the Multi/Single types
# MultiRecord headers need to have consecutive file descriptors
# i.e.
# rec.dat ...
# rec.dat ...
# rec1.dat ...
# rec.dat ... <- ILLEGAL as rec1.dat has appeared before
# I think it i

struct Header{S <: UnionSpecVector}
    record_name::String
    number_of_segments::Union{Nothing,UInt32}
    number_of_signals::UInt32
    sampling_frequency::Float32
    counter_frequency::Float32
    base_counter_value::Float32
    samples_per_signal::Union{Nothing,UInt32}
    base_time::Union{Nothing,Time}
    base_date::Union{Nothing,Date}
    parentdir::String
    signal_specs::S

    function Header(
        record_name,
        number_of_segments,
        number_of_signals,
        sampling_frequency,
        counter_frequency,
        base_counter_value,
        samples_per_signal,
        base_time,
        base_date,
        parentdir,
        signal_specs::T) where {T <: UnionSpecVector}
        new{T}(
            record_name,
            number_of_segments,
            number_of_signals,
            sampling_frequency,
            counter_frequency,
            base_counter_value,
            samples_per_signal,
            base_time,
            base_date,
            parentdir,
            signal_specs,
        )
    end
end

"""
    record_name(h::Header)
a getter method for the record_name field in the header
"""
record_name(h::Header) = getfield(h, :record_name)

"""
    number_of_segments(h::Header)
a getter method for the number_of_segments field in the header
"""
number_of_segments(h::Header) = getfield(h, :number_of_segments)

"""
    number_of_signals(h::Header)
a getter method for the number_of_signals field in the header
"""
number_of_signals(h::Header) = getfield(h, :number_of_signals)

"""
    sampling_frequency(h::Header)
a getter method for the sampling_frequency field in the header
"""
sampling_frequency(h::Header) = getfield(h, :sampling_frequency)

"""
    counter_frequency(h::Header)
a getter method for the counter_frequency field in the header

"""
counter_frequency(h::Header) = getfield(h, :counter_frequency)

"""
    base_counter_value(h::Header)
a getter method for the base_counter_value field in the header
"""
base_counter_value(h::Header) = getfield(h, :base_counter_value)

"""
    samples_per_signal(h::Header)
a getter method for the samples_per_signal field in the header
"""
samples_per_signal(h::Header) = getfield(h, :samples_per_signal)

"""
    base_time(h::Header)
a getter method for the base_time field in the header
"""
base_time(h::Header) = getfield(h, :base_time)

"""
    base_date(h::Header)
a getter method for the base_date field in the header

"""
base_date(h::Header) = getfield(h, :base_date)

"""
    parentdir(h::Header)
a getter method for the parentdir field in the header
"""
parentdir(h::Header) = getfield(h, :parentdir)

"""
    signalspecline(h::Header)
a getter method for the signalspecline field in the header
"""
signalspecline(h::Header) = getfield(h, :signal_specs)

adc_gain(h::Header) = h.signal_specs .|> adc_gain

adc_resolution(h::Header) = h.signal_specs .|> adc_resolution
adc_zero(h::Header) = h.signal_specs .|> adc_zero
baseline(h::Header) = h.signal_specs .|> baseline
block_size(h::Header) = h.signal_specs .|> block_size
byte_offset(h::Header) = h.signal_specs .|> byte_offset

"""
    checksum(h::Header) # retrieves the checksums from a header file
    checksum(h::Header,signal)  # *calculates* the checksum of a decoded symbol
"""
function checksum end
checksum(h::Header) = h.signal_specs |> checksum
description(h::Header) = h.signal_specs |> description
filename(h::Header) = h.signal_specs |> filename
format(h::Header) = h.signal_specs |> format
initial_value(h::Header) = h.signal_specs |> initial_value
samples_per_frame(h::Header) = h.signal_specs |> samples_per_frame
skew(h::Header) = h.signal_specs |> skew
units(h::Header) = h.signal_specs |> units

"""
    nsignals(h::Header)
number of signal specs present in a header
"""
nsignals(h::Header) = length(h.signal_specs)

@inline function _parse(T, v)
    T === String && v isa SubString && return String(v)
    I = T
    if T isa Union
        I = I.a === Nothing ? I.b : I.a
    end
    parse(I, v)
end

function parse_record_line(record_line::String)
    record_regex = r"[\" \t]* (?<record_name>[-\w]+)
         /?(?<number_of_segments>\d*)
         [ \t]+ (?<number_of_signals>\d+)
         [ \t]* (?<sampling_frequency>\d*\.?\d*)
         /*(?<counter_frequency>-?\d*\.?\d*)
         \(?(?<base_counter_value>-?\d*\.?\d*)\)?
         [ \t]* (?<samples_per_signal>\d*)
         [ \t]* (?<base_time>\d{0,2}:?\d{0,2}:?\d{0,2}\.?\d{0,6})
         [ \t]* (?<base_date>\d{0,2}/?\d{0,2}/?\d{0,4})"x
    m = match(record_regex, record_line)
    @assert !isnothing(m) error("invalid record line $(record_line)")
    names = fieldnames(Header)
    types = fieldtypes(Header)
    typelookup = Dict{Symbol,Type}()
    for (name, type) in zip(names, types)
        if type isa Union
            t = type.a !== Nothing ? type.a : type.b
        else
            t = type
        end
        typelookup[name] = t
    end

    parse_errors = Vector{String}[]

    isempty(m[:record_name]) &&
        push!(parse_errors, "missing record name from: $(record_line)")
    isempty(m[:number_of_signals]) &&
        push!(parse_errors, "missing record name from: $(record_line)")
    if !isempty(parse_errors)
        pushfirst!("record line error:")
        push!("record_line")
        error(join(parse_errors, "\n"))
    end
    record_name = String(m[:record_name])
    number_of_segments = if isempty(m[:number_of_segments])
        nothing
    else
        _parse(typelookup[:number_of_segments], m[:number_of_segments])
    end
    number_of_signals = if isempty(m[:number_of_signals])
        nothing
    else
        _parse(typelookup[:number_of_signals], m[:number_of_signals])
    end
    sampling_frequency = if isempty(m[:sampling_frequency])
        DEFAULT_FREQUENCY
    else
        _parse(typelookup[:sampling_frequency], m[:sampling_frequency])
    end
    counter_frequency = if isempty(m[:counter_frequency])
        sampling_frequency
    else
        _parse(typelookup[:counter_frequency], m[:counter_frequency])
    end
    base_counter_value = if isempty(m[:base_counter_value])
        typelookup[:base_counter_value](0)
    else
        _parse(typelookup[:base_counter_value], m[:base_counter_value])
    end
    samples_per_signal =
        if (isempty(m[:samples_per_signal]) || m[:samples_per_signal] == "0")
            nothing
        else
            _parse(typelookup[:samples_per_signal], m[:samples_per_signal])
        end
    base_time = isempty(m[:base_time]) ? nothing : Time(m[:base_time], TIME_FORMAT)
    base_date = isempty(m[:base_date]) ? nothing : DATE(m[:base_date], DATE_FORMAT)

    NamedTuple([
        :record_name => record_name,
        :number_of_segments => number_of_segments,
        :number_of_signals => number_of_signals,
        :sampling_frequency => sampling_frequency,
        :counter_frequency => counter_frequency,
        :base_counter_value => base_counter_value,
        :samples_per_signal => samples_per_signal,
        :base_time => base_time,
        :base_date => base_date,
    ])
end

function parse_signal_spec_line(signal_line::String)::SignalSpecLine
    rx_signal = r"""
        [ \t]* (?<filename>~?[-\w]*\.?[\w]*)
        [ \t]+ (?<format>\d+)
               x?(?<samples_per_frame>\d*)
               :?(?<skew>\d*)
               \+?(?<byte_offset>\d*)
        [ \t]* (?<adc_gain>-?\d*\.?\d*e?[\+-]?\d*)
               \(?(?<baseline>-?\d*)\)?
               /?(?<units>[\w\^\-\?%\/]*)
        [ \t]* (?<adc_resolution>\d*)
        [ \t]* (?<adc_zero>-?\d*)
        [ \t]* (?<initial_value>-?\d*)
        [ \t]* (?<checksum>-?\d*)
        [ \t]* (?<block_size>\d*)
        [ \t]* (?<description>[\S]?[^\t\n\r\f\v]*)
        """x

    m = match(rx_signal, signal_line)
    @assert !isnothing(m) "invalid signal line:\n$signal_line"

    names = fieldnames(SignalSpecLine)
    types = fieldtypes(SignalSpecLine)

    struct_symbols = zip(names, types)
    typelookup = Dict(n => T for (n, T) in struct_symbols)
    data::Dict{Symbol,Any} = Dict(name => nothing for name in names)

    if isempty(m[:filename]) || isempty(m[:format])
        @assert false "missing required fields"
    end

    filename=String(m[:filename])
    format=WfdbFormat(StorageFormat(_parse(Int64, m[:format])))
    samples_per_frame = if isempty(m[:samples_per_frame])
        typelookup[:samples_per_frame](1)
    else
        _parse(typelookup[:samples_per_frame], m[:samples_per_frame])
    end
    skew = isempty(m[:skew]) ? typelookup[:skew](0) : _parse(typelookup[:skew], m[:skew])
    byte_offset = if isempty(m[:byte_offset])
        typelookup[:byte_offset](0)
    else
        _parse(typelookup[:byte_offset], m[:byte_offset])
    end
    adc_gain = if isempty(m[:adc_gain])
        typelookup[:adc_gain](200)
    else
        _parse(typelookup[:adc_gain], m[:adc_gain])
    end
    units = isempty(m[:units]) ? "mV" : _parse(typelookup[:units], m[:units])
    adc_resolution = if isempty(m[:adc_resolution])
        typelookup[:adc_resolution](12)
    else
        _parse(typelookup[:adc_resolution], m[:adc_resolution])
    end

    adc_zero = if isempty(m[:adc_zero])
        typelookup[:adc_zero](0)
    else
        _parse(typelookup[:adc_zero], m[:adc_zero])
    end
    baseline =
        isempty(m[:baseline]) ? adc_zero : _parse(typelookup[:baseline], m[:baseline])

    initial_value = if isempty(m[:initial_value])
        adc_zero
    else
        _parse(typelookup[:initial_value], m[:initial_value])
    end

    checksum = isempty(m[:checksum]) ? nothing : _parse(typelookup[:checksum], m[:checksum])

    block_size = if isempty(m[:block_size])
        typelookup[:block_size](0)
    else
        _parse(typelookup[:block_size], m[:block_size])
    end

    description = String(m[:description])

    SignalSpecLine(
        filename,
        format,
        samples_per_frame,
        skew,
        byte_offset,
        adc_gain,
        baseline,
        units,
        adc_resolution,
        adc_zero,
        initial_value,
        checksum,
        block_size,
        description,
    )
end

function rdheader(path)
    @assert isfile(path)
    f = open(path)
    lines = readlines(f) .|> strip |> filter(!isempty) .|> String
    close(f)
    comments = lines |> filter(contains(r"#.*"))
    lines = lines |> filter(!contains(r"#.*"))
    recordline = parse_record_line(popfirst!(lines))

    signal_spec_lines = Vector{SignalSpecLine}(undef, length(lines))
    for (idx, line) in enumerate(lines)
        signal_spec_lines[idx] = parse_signal_spec_line(line)
    end

    filenames = filename(signal_spec_lines)
    uniquefilenames = unique(filenames)
    #if there is only one filename in file it is a SingleSpecVec
    #otherwise validation will need to happen
    spec_type = length(uniquefilenames) == 1 ? SingleSpecVector : MultiSpecVector

    if spec_type === MultiSpecVector
        v = spec_type()
        seen_filenames = Set()
        for (i,ufn) in enumerate(uniquefilenames)
            t = SingleSpecVector()
            ufn ∈ seen_filenames && error("non-contiguous filenames specified")
            while !isempty(signal_spec_lines) && filename(signal_spec_lines[1]) == ufn
                push!(t, popfirst!(signal_spec_lines))
            end
            push!(v,t)
            push!(seen_filenames,ufn)
        end
        signal_spec_lines = v
    end

    parentdir = splitdir(path)[1]
    Header(
        recordline[:record_name],
        recordline[:number_of_segments],
        recordline[:number_of_signals],
        recordline[:sampling_frequency],
        recordline[:counter_frequency],
        recordline[:base_counter_value],
        recordline[:samples_per_signal],
        recordline[:base_time],
        recordline[:base_date],
        parentdir,
        signal_spec_lines,
    )
end
