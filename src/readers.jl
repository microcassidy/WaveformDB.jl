function read_binary(io::IO,header::Header, F::WfdbFormat{T}) where {T <: AbstractStorageFormat}
    n_samples = sum(samples_per_frame(header) * samples_per_signal(header))
    return read_binary(io, n_samples, F)
end
function read_binary(io::IO,s::SingleSpecVector, n_samples::UInt64, ::WfdbFormat{format311})::Vector{Int64}
    m = n_samples % 3 #number of samples that dont fit into a U32
    nchunk = Int64(floor(n_samples / 3))
    chunksizebytes = 4
    bytelength_actual = chunksizebytes * nchunk + 2m

    if m > 0
        added_samples = 3 - m
        nchunk += 1
    end
    data = zeros(UInt8, chunksizebytes*nchunk) #zero-padded
    datav = @view data[1:bytelength_actual] #read-region

    read!(io, datav)

    data = reinterpret(UInt32, data)
    output = Vector{Int16}(undef, n_samples + added_samples)

    @inline twos_complement(p) = p > 511 ? p - 1024 : p

    function mask_shift!(output, idx, x)
        val = reinterpret(Int16, UInt16(x & (0x03FF)))
        output[idx] = twos_complement(val)
        x >>= 10
        return x
    end
    for block in eachindex(data)
        x = data[block]
        for j in 1:3
            idx = 3(block - 1) + j
            x = mask_shift!(output, idx, x)
        end
    end
    resize!(output, n_samples)
    convert(Vector{Int64}, output)
end

function read_binary(io::IO,s::SingleSpecVector, n_samples::UInt64, ::WfdbFormat{format8})::Vector{Int64}
    n_signals = length(s)
    data = Vector{Int8}(undef, n_samples)
    output = Vector{Int16}(undef, n_samples)
    read!(io, data)
    # acc = zeros(Int16, n_signals)
    acc = initial_value(s)
    blocks = Int(n_samples / n_signals)
    for j in 1:blocks
        for i in 1:n_signals
            acc[i] += data[i + (j - 1)n_signals]
            output[i + (j - 1)n_signals] = acc[i]
        end
    end
    convert(Vector{Int64}, output)
end

function read_binary(io::IO,s::SingleSpecVector, n_samples::UInt64, ::WfdbFormat{format16})::Vector{Int64}
    bytes_per_sample = 2
    n_bytes = Int64(n_samples * bytes_per_sample)
    output = Vector{Int16}(undef, n_samples)
    read!(io, output)
    return convert(Vector{Int64}, output)
end

function read_binary(io::IO,s::SingleSpecVector, n_samples::UInt64, ::WfdbFormat{format24})::Vector{Int64}
    bytespersample = 3
    nbytes = n_samples * bytespersample

    MAX_POS = Int32(2^(24 - 1) - 1)
    M = Int32(2^(24))
    @inline twos_complement(v::Int32) = v > MAX_POS ? v - M : v

    buffer = Vector{UInt8}(undef, nbytes)
    read!(io, buffer)
    buffer = convert(Vector{Int32}, buffer)
    for n in 1:n_samples
        @inbounds buffer[n] = twos_complement(
            buffer[3n - 2] + (buffer[3n - 1] << 8) + (buffer[3n] << 16)
        )
    end
    resize!(buffer, n_samples)
    return convert(Vector{Int64}, buffer)
end

function read_binary(io::IO,s::SingleSpecVector, n_samples::UInt64, ::WfdbFormat{format310})::Vector{Int64}

    N = Int64(floor(n_samples / 3))
    m = n_samples % 3 #processing 3 samples per iteration

    added_samples = 0
    true_bytes = N * 4 + 2(m) #the real amount of bytes to store the data

    if m != 0
        added_samples = Int64(3 - m)
        N += 1
    end

    n_bytes = Int64(4*N)
    data = zeros(UInt8, n_bytes)
    datav = @view data[1:true_bytes] #for filling whilst leaving the zero padding
    read!(io, datav)

    output = Vector{Int16}(undef, n_samples + added_samples)

    @inline p0(x0, x1) = (x0 >> 1) + (x1 & 0x7) << 7
    @inline p1(x2, x3) = p0(x2, x3) #restatement of the first pair
    @inline p2(x1, x3) = (x1 >> 3) & 0x1F + (((x3 >> 3) & 0x1F) << 5)
    @inline twos_complement(p) = p > 511 ? p - 1024 : p

    for idx in 1:N
        x0 = Int16(data[idx * 4 - 3])
        x1 = Int16(data[idx * 4 - 2])
        x2 = Int16(data[idx * 4 - 1])
        x3 = Int16(data[idx * 4])
        # x0,x1,x2,x3 = Int16.(data[idx*4 - 3 : idx * 4])
        _p0 = p0(x0, x1) |> twos_complement
        _p1 = p1(x2, x3) |> twos_complement
        _p2 = p2(x1, x3) |> twos_complement
        output[3 * idx - 2] = _p0
        output[3 * idx - 1] = _p1
        output[3 * idx] = _p2
    end
    resize!(output, n_samples)
    convert(Vector{Int64}, output)
end

function read_binary(io::IO,s::SingleSpecVector, n_samples::UInt64, ::WfdbFormat{format160})::Vector{Int64}
    data = Vector{UInt16}(undef, n_samples)
    read!(io, data)
    data = convert(Vector{Int32}, data)
    for idx in eachindex(data)
        @inbounds data[idx] -= 32_768
    end
    return data
end

function read_binary(io::IO,s::SingleSpecVector, n_samples::UInt64, ::WfdbFormat{format80})::Vector{Int64}
    data = convert(Vector{Int32}, read(io, n_samples; all=false))
    for idx in eachindex(data)
        @inbounds data[idx] -= Int32(128)
    end
    return convert(Vector{Int64}, data)
end

function read_binary(io::IO,s::SingleSpecVector, n_samples::UInt64, ::WfdbFormat{format61})::Vector{Int64}
    bytes_per_sample = 2
    n_bytes = Int64(n_samples * bytes_per_sample)
    output = Vector{Int16}(undef, n_samples)
    read!(io, output)
    return bswap.(output)
end

function read_binary(io::IO,s::SingleSpecVector, n_samples::UInt64, ::WfdbFormat{format32})::Vector{Int64}
    bytes_per_sample = 4
    n_bytes = Int64(n_samples * bytes_per_sample)
    output = Vector{Int32}(undef, n_samples)
    read!(io, output)
    return convert(Vector{Int64}, output)
end

function read_binary(io::IO,s::SingleSpecVector, n_samples::UInt64, ::WfdbFormat{format212})::Vector{Int64}
    n_bytes = Int64(ceil(n_samples * 3//2))
    true_bytes = n_bytes
    m = n_samples % 2
    N = Int64(floor(n_samples / 2))
    if m % 2 == 1
        n_bytes += 2
        N += 1
    end
    output = Vector{Int16}(undef, n_samples + m)
    data_buffer = zeros(UInt8, n_bytes)
    datav = @view data_buffer[1:true_bytes]

    read!(io, datav)

    @inline p1(x0, x1) = muladd(x1 & 0x0F, 256, x0)
    @inline p2(x1, x2) = muladd(x1 & 0xF0, 16, x2)
    @inline twos_complement(p) = p > 2047 ? p - 4096 : p

    for idx in 1:N
        # read!(io,buf)
        @inbounds x0 = Int16(data_buffer[3(idx) - 2])
        @inbounds x1 = Int16(data_buffer[3(idx) - 1])
        @inbounds x2 = Int16(data_buffer[3(idx)])
        _p1 = p1(x0, x1) |> twos_complement
        _p2 = p2(x1, x2) |> twos_complement
        @inbounds output[2 * idx - 1] = _p1
        @inbounds output[2 * idx] = _p2
    end
    resize!(output, length(output) - m)
    convert(Vector{Int64}, output)
end

function dac!(samples::AbstractVector{T}, h::Header) where {T <: Float64}
    baselines = baseline(h)
    initialvalues = initial_value(h)

    adcgains = adc_gain(h)
    _samples_per_frame = samples_per_frame(h)
    _nsignals = nsignals(h)

    @assert all(_samples_per_frame .== 1)
    samples_per_signal = Int64(length(samples) / _nsignals)
    linearindex = LinearIndices(reshape(samples, _nsignals, :))
    nrow, ncol = size(linearindex)
    for j in 1:ncol
        for i in 1:nrow
            @inbounds idx = linearindex[i, j]
            @inbounds samples[idx] -= baselines[i]
            @inbounds samples[idx] /= adcgains[i]
        end
    end
end
